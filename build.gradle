/**
 * 1.0.0   : release
 * 1.1.x   : 自動デプロイに移行
 * 1.2.x   : ログの種類ごとにオプションを切れるように修正
 */
def ARTIFACT_VERSION = "1.2"
def REPOSITORY_NAME = "java-libraries"
def BINTRAY_LICENSES = ["MIT"]
def BINTRAY_LABELS = ['android']
def BINTRAY_API_KEY = System.env.BINTRAY_API_KEY
def BINTRAY_GPG_PASS = System.env.BINTRAY_GPG_PASS

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

repositories {
    jcenter()
}

apply plugin: 'java'

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = "com.eaglesakura"

dependencies {
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = "UTF-8"
    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/**
 * バージョンを取得する
 * CircleCIの場合、バージョン末尾にビルド番号を付与する
 */
def getArtifactVersionSuffix() {
    if (System.env.CIRCLE_BUILD_NUM != null) {
        // CircleCIのバージョンが指定されているので、そちらを利用する
        return ".${System.env.CIRCLE_BUILD_NUM}"
    } else {
        return ".snapshot"
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = 'eaglesakura'
    key = BINTRAY_API_KEY
    pkg {
        repo = REPOSITORY_NAME
        name = new File("./").name
        licenses = BINTRAY_LICENSES
        labels = BINTRAY_LABELS
        vcsUrl = 'https://github.com/eaglesakura/'
        version {
            name = ARTIFACT_VERSION + getArtifactVersionSuffix()
            released = new Date()
            gpg {
                sign = true
                passphrase = BINTRAY_GPG_PASS
            }
        }
    }
    publications = ['Publication']
    configurations = ['archives']
}
